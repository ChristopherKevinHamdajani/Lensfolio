import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id 'org.springframework.boot' version '2.6.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'jacoco'
    id "org.sonarqube" version "3.3"
    id 'se.thinkcode.cucumber-runner' version '0.0.8'
}

group = 'nz.ac.canterbury.team-100'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

wrapper {
    gradleVersion = '7.4'
}

ext {
    junitVersion = '5.7.2'
    cucumberVersion = '6.10.4'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'net.devh:grpc-client-spring-boot-starter:2.13.1.RELEASE' // GRPC client dependencies
    implementation 'nz.ac.canterbury.seng302:shared:1.0' // Local package containing .proto files

    implementation 'io.jsonwebtoken:jjwt-api:0.11.0'

    // Java Persistence API
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.6.4'

    implementation 'org.mariadb.jdbc:mariadb-java-client:2.7.3'

    implementation 'org.webjars.npm:fullcalendar:5.10.2'
    implementation 'org.webjars.npm:fullcalendar__bootstrap5:5.10.2'
    testImplementation 'org.mockito:mockito-core:4.3.1'


    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation 'org.webjars.npm:jquery:3.6.0'

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

    testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"

    testImplementation "io.cucumber:cucumber-junit:${cucumberVersion}"
    testImplementation "org.junit.vintage:junit-vintage-engine:${junitVersion}"

    testImplementation 'org.mockito:mockito-inline:2.13.0'

    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '4.1.3'

    testImplementation group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '5.1.1'

    testImplementation group: 'ru.yandex.qatools.ashot', name: 'ashot', version: '1.5.4'

    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.3.0-rc1'

}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumberCli() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin', 'pretty',
                    '--plugin', 'html:target/cucumber-report.html',
                    '--glue', 'nz.ac.canterbury.seng302.portfolio.cucumber',
                    'src/test/resources']
        }
    }
}

cucumber {
    main = 'io.cucumber.core.cli.Main'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    testLogging {
        events TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED
    }
    filter{
        includeTestsMatching "nz.ac.canterbury.seng302.portfolio.controller.*"

        includeTestsMatching "nz.ac.canterbury.seng302.portfolio.model.*"

        includeTestsMatching "nz.ac.canterbury.seng302.portfolio.service.*"

        includeTestsMatching "nz.ac.canterbury.seng302.portfolio.utility.*"

    }

    systemProperties(project.gradle.startParameter.systemPropertiesArgs)
}

// Jacoco test coverage configuration
jacoco {
    toolVersion = "0.8.7"
}

// select what reports Jacoco generates and where to put them
jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/jacoco/test-report.xml")
        csv.enabled false
        html.destination file("${buildDir}/jacoco/html")
    }
}

// Sonarqube specific configuration (task)
sonarqube {
    properties {
        property "sonar.projectKey", "team-100-portfolio"
        property "sonar.projectName", "team 100-portfolio"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.host.url", "https://sonarqube.csse.canterbury.ac.nz"
        property "sonar.login", "$System.env.SONARQUBE_API_KEY"
    }
}